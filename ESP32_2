#include <WiFi.h>
#include <WiFiClientSecure.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>
#include "time.h"

// Configurações da rede Wi-Fi (2.4 GHz)
const char* ssid = "Mariane";
const char* password = "mari9515";

// Pino do relé
const int relePin = 5;

// Intervalo entre consultas ao servidor (ms)
unsigned long ultimoPing = 0;
const unsigned long intervalo = 1000; // 10 segundos

// Configuração de NTP e fuso horário
const char* ntpServer = "pool.ntp.org";
const long gmtOffset_sec = 0 * 3600;   // UTC-4 (Mato Grosso)
const int daylightOffset_sec = 0;       // sem horário de verão


void setup() {
  pinMode(relePin, OUTPUT);
  digitalWrite(relePin, LOW);

  Serial.begin(115200);
  Serial.println("ESP32 WROVER Kit inicializando...");

  // Conecta ao Wi-Fi
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }

  Serial.println("\nWiFi conectado!");
  Serial.print("IP do ESP32: ");
  Serial.println(WiFi.localIP());

  // Configura NTP e fuso horário
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
  Serial.println("Sincronizando hora via NTP...");

  struct tm timeinfo;
  if (!getLocalTime(&timeinfo)) {
    Serial.println("Falha ao obter hora");
    return;
  }
  Serial.println(&timeinfo, "Hora local: %d/%m/%Y %H:%M:%S");
}

void loop() {
  unsigned long agora = millis();

  // Se perder conexão, tenta reconectar
  if (WiFi.status() != WL_CONNECTED) {
    Serial.println("WiFi desconectado, tentando reconectar...");
    WiFi.reconnect();
    while (WiFi.status() != WL_CONNECTED) {
      delay(1000);
      Serial.print(".");
    }
    Serial.println("\nWiFi reconectado!");
  }

  // Consulta ao servidor a cada 10 segundos
  if (agora - ultimoPing >= intervalo) {
    ultimoPing = agora;

    WiFiClientSecure client;
    client.setInsecure(); // ignora verificação de certificado SSL

    HTTPClient http;
    String url = "https://carlitoslocacoes.com/Demo3/virada.php?id=21&tag=TESLA";
    http.begin(client, url);

    int httpCode = http.GET();
    if (httpCode == 200) {
      String payload = http.getString();
      Serial.println("Resposta: " + payload);

      StaticJsonDocument<1024> doc;
      DeserializationError error = deserializeJson(doc, payload);

      if (!error) {
        JsonObject obj = doc.is<JsonArray>() ? doc[0] : doc.as<JsonObject>();

        int statusAtual = obj["status_atual"];
        float saldo = obj["saldo"];
        const char* mensagem = obj["mensagem"];

        Serial.println("Status atual: " + String(statusAtual));
        Serial.println("Saldo: " + String(saldo));
        if (mensagem) Serial.println("Mensagem: " + String(mensagem));

        if (statusAtual == 1 && saldo > 0) {
          digitalWrite(relePin, HIGH);
          Serial.println("Relé ligado");
        } else {
          digitalWrite(relePin, LOW);
          Serial.println("Relé desligado");
        }
      } else {
        Serial.println("Erro ao interpretar JSON");
      }
    } else {
      Serial.println("Erro HTTP: " + String(httpCode));
    }
    http.end();

    // Mostra hora local atual
    struct tm timeinfo;
    if (getLocalTime(&timeinfo)) {
      Serial.println(&timeinfo, "Hora local: %d/%m/%Y %H:%M:%S");
    }
  }
}
